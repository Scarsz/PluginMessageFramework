defaultTasks 'clean', 'build', 'test', 'publishToMavenLocal'

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'checkstyle'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    getProject().group = 'com.ikeirnez.pluginmessageframework'
    getProject().version = "1.0.0-SNAPSHOT"

    repositories {
        mavenCentral()
        mavenLocal()
    }

    checkstyle {
        toolVersion = 6.5
        configFile = new File(rootDir, "checkstyle.xml")
        configProperties = [
                'basedir': project.projectDir,
                'suppressions': new File(rootDir, "checkstyle-suppressions.xml"),
                'severity': 'warning'
        ]
    }

    tasks.jar.dependsOn "check"

    compileJava {
        targetCompatibility = JavaVersion.VERSION_1_7
        sourceCompatibility = JavaVersion.VERSION_1_7
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

}

task generateAggregatedJavadocs(type: Javadoc) {
    source = subprojects.collect { project -> project.sourceSets.main.allJava }
    classpath = files(subprojects.collect {project -> project.sourceSets.main.compileClasspath})
    excludes = ['**/internal/**', '**/Impl*']
    destinationDir = new File(projectDir, 'docs')
}